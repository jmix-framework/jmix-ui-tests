plugins {
    id 'io.jmix' version '1.3.0-M1'
    id 'java'
    id 'groovy'
}

apply plugin: 'org.springframework.boot'

jmix {
    bomVersion = '1.3.999-SNAPSHOT'
}

group = 'io.jmix.tests'

repositories {
    mavenCentral()
    maven {
        url 'https://nexus.jmix.io/repository/public'
    }
    maven {
        url = 'https://nexus.jmix.io/repository/premium'
        credentials {
            username(rootProject.hasProperty('premiumRepoUser') ? rootProject['premiumRepoUser'] : project.getProperty('jmixPremiumRepoUser'))
            password(rootProject.hasProperty('premiumRepoPass') ? rootProject['premiumRepoPass'] : project.getProperty('jmixPremiumRepoPassword'))
        }
    }
}

dependencies {
    implementation 'io.jmix.core:jmix-core-starter'
    implementation 'io.jmix.data:jmix-eclipselink-starter'
    implementation 'io.jmix.ui:jmix-ui-starter'
    implementation 'io.jmix.ui:jmix-ui-data-starter'
    implementation 'io.jmix.ui:jmix-ui-themes-compiled'
    implementation 'io.jmix.security:jmix-security-starter'
    implementation 'io.jmix.security:jmix-security-ui-starter'
    implementation 'io.jmix.security:jmix-security-data-starter'
    implementation 'io.jmix.dynattr:jmix-dynattr-starter'
    implementation 'io.jmix.dynattr:jmix-dynattr-ui-starter'
    implementation 'io.jmix.audit:jmix-audit-starter'
    implementation 'io.jmix.audit:jmix-audit-ui-starter'
    implementation 'io.jmix.datatools:jmix-datatools-starter'
    implementation 'io.jmix.datatools:jmix-datatools-ui-starter'
    implementation 'io.jmix.localfs:jmix-localfs-starter'

    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation('org.jsoup:jsoup') {
        version {
            strictly '1.11.2'
        }
    }
    runtimeOnly 'org.hsqldb:hsqldb'

    testImplementation "io.jmix.masquerade:jmix-masquerade:1.1.0-SNAPSHOT"

    testImplementation 'com.codeborne:selenide:6.6.1'

    testImplementation 'org.codehaus.groovy:groovy:3.0.10'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'

    testImplementation 'org.testcontainers:selenium:1.16.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.16.3'
    testImplementation 'org.testcontainers:postgresql:1.16.3'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
    }

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testRuntimeOnly 'org.postgresql:postgresql:42.3.3'
    implementation "io.jmix.reports:jmix-reports-starter"
    implementation "io.jmix.reports:jmix-reports-ui-starter"
    implementation "io.jmix.reports:jmix-reports-rest-starter"
    widgets "io.jmix.ui:jmix-charts-widgets"
    widgets "io.jmix.ui:jmix-ui-widgets"
    implementation "io.jmix.ui:jmix-charts-starter"
    widgets "io.jmix.ui:jmix-pivot-table-widgets"
    implementation "io.jmix.ui:jmix-pivot-table-starter"
    implementation("io.jmix.webdav:jmix-webdav-rest-starter")
    implementation("io.jmix.webdav:jmix-webdav-starter")
    implementation("io.jmix.webdav:jmix-webdav-ui-starter")
}

task testUi(type: Test) {
    useJUnitPlatform()

    filter {
        includeTestsMatching "io.jmix.tests.ui.*"
    }

    if (rootProject.hasProperty('jmix.tests.ui.baseHost')) {
        systemProperty 'jmix.tests.ui.baseHost', rootProject['jmix.tests.ui.baseHost']
    } else {
        systemProperty 'jmix.tests.ui.baseHost', 'http://host.docker.internal'
    }

    if (rootProject.hasProperty('selenide.browser')) {
        systemProperty 'selenide.browser', rootProject['selenide.browser']
    }

    if (rootProject.hasProperty('webdriver.chrome.driver')) {
        systemProperty 'webdriver.chrome.driver', rootProject['webdriver.chrome.driver']
    }

    if (rootProject.hasProperty('webdriver.gecko.driver')) {
        systemProperty 'webdriver.gecko.driver', rootProject['webdriver.gecko.driver']
    }

    def envProperties = rootProject.getProperties()
    for (def entry in envProperties.entrySet()) {
        if (String.valueOf(entry.key).startsWith('selenide.')) {
            systemProperty(String.valueOf(entry.key), entry.value)
        }
    }
}

task testSampler(type: Test) {
    useJUnitPlatform()

    filter {
        includeTestsMatching "io.jmix.tests.sampler.*"
    }

    if (rootProject.hasProperty('jmix.tests.sampler.baseUrl')) {
        systemProperty 'selenide.baseUrl', rootProject['jmix.tests.sampler.baseUrl']
    } else {
        systemProperty 'selenide.baseUrl', 'http://host.docker.internal:8080/sampler'
    }

    if (rootProject.hasProperty('selenide.browser')) {
        systemProperty 'selenide.browser', rootProject['selenide.browser']
    }

    if (rootProject.hasProperty('webdriver.chrome.driver')) {
        systemProperty 'webdriver.chrome.driver', rootProject['webdriver.chrome.driver']
    }

    if (rootProject.hasProperty('webdriver.gecko.driver')) {
        systemProperty 'webdriver.gecko.driver', rootProject['webdriver.gecko.driver']
    }

    def envProperties = rootProject.getProperties()
    for (def entry in envProperties.entrySet()) {
        if (String.valueOf(entry.key).startsWith('selenide.')) {
            systemProperty(String.valueOf(entry.key), entry.value)
        }
    }
}

test.dependsOn testUi, testSampler
compileWidgets {
    generate "io.jmix.tests.widgets.CustomWidgetSet"
}
